/*******************************************************************************
 * Copyright 2009-2020 Exactpro (Exactpro Systems Limited)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.5.31'
    id 'application'
    id 'com.palantir.docker' version '0.25.0'
}

ext {
    dockerImageVersion = release_version
    cradleApiVersion = '3.1.2'
}

group 'com.exactpro.th2'
version release_version

sourceCompatibility = 11
targetCompatibility = 11

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = "11"
    }
}

repositories {
    mavenCentral()
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }
}

dependencies {
    api platform('com.exactpro.th2:bom:3.0.0')

    implementation 'org.slf4j:slf4j-api'
    implementation 'org.slf4j:slf4j-log4j12'

    implementation 'com.exactpro.th2:common:3.34.0'

    implementation 'org.eclipse.jetty:jetty-server:9.4.44.v20210927'
    implementation 'org.eclipse.jetty:jetty-servlet:9.4.44.v20210927'


    implementation "com.exactpro.th2:cradle-cassandra:$cradleApiVersion"
    implementation('net.jpountz.lz4:lz4:1.3.0') {
        because('cassandra driver requires lz4 impl in classpath for compression')
    }
    implementation 'com.exactpro.th2:grpc-data-provider:1.1.0'

    implementation 'io.github.microutils:kotlin-logging:2.1.14'

    implementation('io.prometheus:simpleclient') {
        because('need add custom metrics to provider')
    }

    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation('org.jetbrains.kotlin:kotlin-reflect') {
        because('consistent version of kotlin in classpath. Jackson uses 1.3.72 right now')
    }

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'org.assertj:assertj-core:3.12.2'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
            'Created-By': "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
            'Specification-Title': '',
            'Specification-Vendor': 'Exactpro Systems LLC',
            'Implementation-Title': project.archivesBaseName,
            'Implementation-Vendor': 'Exactpro Systems LLC',
            'Implementation-Vendor-Id': 'com.exactpro',
            'Implementation-Version': project.version
        )
    }
}

applicationName = 'service'

distTar {
    archiveName "${applicationName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}


compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

application {
    mainClassName = "com.exactpro.th2.lwdataprovider.MainKt"
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}
